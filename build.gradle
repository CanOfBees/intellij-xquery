/*
 * Copyright 2013 Grzegorz Ligas
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */









apply plugin: 'java'
sourceCompatibility = 1.6

project.ext.ideaInstallationPath = 'sdk/idea-IC-129.713'

sourceSets {
    main {
        java {
            srcDirs 'gen'
        }
    }
}

processResources {
    from(projectDir) {
        include 'META-INF/**'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: project.ext.ideaInstallationPath + '/lib', include: '*.jar')
    compile files("${System.properties['java.home']}/../lib/tools.jar")
}

test {
    jvmArgs = ['-Didea.home.path=sdk/idea-IC-129.713', '-Didea.test.group=ALL_EXCLUDE_DEFINED', '-Didea.platform.prefix=Idea']
}

metaInf << files('/META-INF/plugin.xml')

task downloadSdk(type: DownloadTask) {
    sourceUrl = 'http://download.jetbrains.com/idea/ideaIC-12.1.4.tar.gz'
    target = file('sdk/ideaIC-12.1.4.tar.gz')
}

task unzipSdk << {
    copy {
        from tarTree(resources.gzip('sdk/ideaIC-12.1.4.tar.gz'))
        into file('sdk/')
    }
}

class DownloadTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}